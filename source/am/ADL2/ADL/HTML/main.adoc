= ADL - Archetype Definition Language

== Introduction

This section describes whole ADL artefacts. The relationship of the cADL-encoded ` definition` section and the ODIN-encoded ` terminology` section is discussed in detail. In this section, only standard ADL (i.e. the standard cADL constructs and types described so far) is assumed.

Some syntax valdity rules are defined, but validity in general is defined by the rules stated in the AOM specification, which can be checked by a compiler as soon as an AOM structure is parsed from an ADL document (or other serialsiation format).

The general structure of ADL artefacts is as follows:

--------------------------------------------------------
    (([flat] archetype | template | template_overlay) | 
                operational_template) (qualifiers)
        id
    [specialize 
        parent_id]
    language
        ODIN
    description 
        ODIN
    definition 
        cADL
    [rules 
        assertions]
    terminology 
        ODIN
    [annotations 
        ODIN]
    [revision_history 
        ODIN]
--------------------------------------------------------

An ADL source template has the structure (‘template’ keyword; must be specialised):

--------------------------------
    [flat] template (qualifiers)
        id
    specialize 
        parent_id
    language
        ODIN
    description 
        ODIN
    definition 
        cADL
    [rules 
        assertions]
    terminology 
        ODIN
    [annotations 
        ODIN]
    [revision_history 
        ODIN section]
--------------------------------

An ADL template overlay has the structure shown below (‘template_overlay’ keyword, must be specialised; minimal sections):

---------------------------------
    template_overlay (qualifiers)
        id
    specialize 
        parent_id
    definition 
        cADL
    terminology 
        ODIN
---------------------------------

The structure of an operational template expressed in native ADL is as follows (‘operational_template’ keyword; full flattened structure, including component_ontologies section):

-------------------------------------
    operational_template (qualifiers)
        template_id
    language
        ODIN
    description 
        ODIN
    definition 
        cADL
    [rules 
        assertions]
    terminology 
        ODIN
    [annotations 
        ODIN]
    component_ontologies 
        ODIN
-------------------------------------

== File-naming Convention

Up until ADL 1.4, archetypes were expressed in ‘flat’ form ADL and were saved in files with the extension ‘.adl’. These are now treated as legacy flat format files. Beginning with ADL 1.5, the source file format of an archetype is the ‘differential’ form, with the extension ‘.adls’. The flat format being generated by tools, and now has the extension ‘.adlf’.

For specialised archetypes, differential form follows the object-oriented convention and only includes overridden or new elements but not unchanged inherited elements. Inherited elements are determined by compiling and ‘flattening’ a differential archetype with respect to the parent archetypes in its _inheritance lineage_ .

For top-level archetypes the full structure is included in the differential file, but internal references, where they exist, are expanded out in the flat form.

== Basics

=== Keywords

ADL has a small number of keywords which are reserved for use in archetype declarations, as follows:

* `archetype`, `template`, `template_overlay`, `operational_template`,
* `specialise` / `specialize`,
* `concept` ,
* `language` ,
* `description` , `definition` , `rules` , `terminology`
* `annotations`

All of these words can safely appear as identifiers in the ` definition` and ` terminology` sections.

Deprecated keywords include:

* `invariant` -- replaced by `rules`
* `ontology` -- replaced by `terminology`
* `concept` -- obsolete

=== Artefact declaration

The first word in a source ADL archetype declares the artefact type, and is one of the following keywords:

* `archetype`: signifies an archetype;
* `template`: signifies a template;
* `template_overlay`: signifies an overlay component of a template.

The flattened form of any of the above types starts with the keyword ‘flat’ followed by the artefact type.

A fourth artefact type is also possible.

* `operational_archetype`: signifies an operational archetype, generated by flattening a template.

=== Node Identifier Codes

In the `definition` section of an ADL archetype, a specific set of codes is used as node identifiers. Identifier codes always appear in brackets (`[]` ), and begin with the ‘id’ prefix. Specialisations of locally coded concepts have the same root, followed by ‘dot’ extensions, e.g. `[id10.2]` . From a terminology point of view, these codes have no implied semantics - the ‘dot’ structuring is used as an optimisation on node identification.

=== Local Term Codes

In the `definition` section of an ADL archetype, a second set of codes is used for terms denoting constraints on coded items. Term codes are either local to the archetype, or from an external lexicon. This means that the archetype description is the same in all languages, and is available in any language that the codes have been translated to. All term codes are shown in brackets (`[]`) and are prefixed with "at", e.g. `at10` . Codes of any length are acceptable in ADL archetypes. Specialisations of locally coded concepts have the same root, followed by ‘dot’ extensions, e.g. `at10.2` . From a terminology point of view, these codes have no implied semantics - the ‘dot’ structuring is used as an optimisation on node identification.

=== Local Constraint Codes

A third kind of local code is used to stand for constraints on code text items in the body of the archetype. Although these could be included in the main archetype body, because they are language- and/or terminology-sensitive, they are defined in the terminology section, and referenced by codes prefixed by "ac", e.g. `[ac9]` .

== Archetype Identification Section

This section introduces the archetype with the keyword ‘archetype’ ‘template’, ‘template_overlay’ or ‘operational_archetype’, followed by a number of items of meta-data in parentheses, and on the next line, a human-readable archetype identifier. The following shows an identification section with all possible meta-data items.

-------------------------------------------------------------------
    archetype (adl_version=2.0.5; 
                provenance_id=15E82D77-7DB7-4F70-8D8E-EED6FF241B2D;
                build_id=E163E472-3E90-409C-9803-0668A7DA48CE;
                generated;
                controlled)
        openEHR-EHR-OBSERVATION.haematology_result.v0.1.9
-------------------------------------------------------------------

=== ADL Version Indicator

An ADL version identifier is mandatory in all archetypes, and is expressed as a string of the form `adl_version=N.M` , or `N.M.P` , where `N.M[.P]` is the ADL release identifier.

=== Machine Identifiers

A unique identifier for the archetype in the form of a GUID can be specified using the syntax below:

----
    archetype (adl_version=2.0.5; uid=15E82D77-7DB7-4F70-8D8E-EED6FF241B2D)
----

This identifier is set at initial creation or at any time later, and nevre subsequently changes. It acts as an identifier for the physical artefact, regardless of what semantics are changed, including changes to the constituent parts of the multi-axial identifier.

=== Namespaces

A namespaced archetype will have an identification section like the following examples:

-
    archetype (adl_version=2.0.5)
        br.gov.saude::openEHR-EHR-OBSERVATION.haematology_result.v1.0.0
     
    template (adl_version=2.0.5)
        uk.org.primary_care::openEHR-EHR-OBSERVATION.haematology_result.v1.2.15
     
    archetype (adl_version=2.0.5)
        org.openehr::openEHR-EHR-OBSERVATION.haematology_result.v3.22.125-rc.7
-

Namespaces are used to distinguish locally created artefacts representing a given concept (such as ‘haematology result’) from an artefact created elsewhere intended to represent the same concept.

Once a namespace is attached to an archetype, it is considered a part of the identifier, and never changed, even if the archetype moves to a new publishing organisation. This ensures the constant relationship between archetypes and the data created using them.

=== Human Readable Archetype Identifier

The archetype identifier may include a namespace, in the form of a reverse domain name, which denotes the original authoring organisation. The lack of a namespace in the identifier indicates an ad hoc, uncontrolled artefact, not formally associated with any organisation, typical for experimental archetypes, and pre-ADL 1.5 archetypes not yet upgraded to have a namespace. The main part of the identifier is multi-axial concept identifier.

A typical identification sectionfor an ad hoc archetype is as follows:

---------------------------------------------------------
    archetype (adl_version=2.0.5)
        openEHR-EHR-OBSERVATION.haematology_result.v0.1.9
---------------------------------------------------------

The multi-axial archetype identifier identifies archetypes in a global concept space within a given namespace. It is also known as an ‘ontological’ identifier, since the concept space can be understood as an ontology of informational concepts on which the archetypes are based. The syntax of the identifier is described in the Identification section of the openEHR Support IM specification. The structure of the concept space is essentially two-level, with the first level being a reference model class (e.g. openEHR `OBSERVATION` class) and the second being a domain concept (e.g. ‘haematology result’).

Because namespaces are usually treated hierarchically, higher level namespaces (e.g. ‘.org’ domains) are assumed to be includable by more local namespaces, with the result that the concept definition space is inherited as well.

The semantics of the ontological identifier are described in detail in the http://www.openehr.org/releases/trunk/architecture/am/knowledge_id_system.pdf[Knowledge Artefact Identification specification].

=== Specialised Archetype Identification

The archetype identifier of any specialised archetype, including all templates, follows the same rules as for non-specialised archetypes.

=== Version Identifiers

ADL 2 Archetypes contain 3-part version identifiers, with optional qualifiers, following the openEHR Artefact Knowledge Identification specification. Examples below:

--------------------------------------------------------------------------
    br.ms::openEHR-EHR-OBSERVATION.haematology_result.v1.0.0
    br.ms::openEHR-EHR-OBSERVATION.haematology_result.v1.2.15-alpha.45
    br.ms::openEHR-EHR-OBSERVATION.haematology_result.v3.22.125-rc.7
--------------------------------------------------------------------------

The version identifier variants are summarised as follows:

* ` N.M.P` - 3-part version id with no qualifier indicates major.minor.path version
* ` N.M.P-alpha.N` - a `-alpha.N` qualifier indicates uncontrolled changes on `N.M.P` , leading to a new version that is yet to be decided
* ` N.M.P-rc.N` - a `-rc.N` qualifier indicates a release candidate.

=== Validity

The following syntax validity rule applies in the identification section:

SARID: archetype identifier validity. the identifier of the artefact must conform to the ARCHETYPE_ID identifier syntax defined in the openEHR.Support IM Specification.

=== Generated Indicator

A flag indicating whether the archetype was generated or authored can be included after the version, as follows:

---------------------------------------------------------------
    archetype (adl_version=2.0.5; generated)
        org.openehr::openEHR-EHR-OBSERVATION.haematology.v1.2.0
---------------------------------------------------------------

This marker is used to support the migration to differential archetype representation introduced in ADL 1.5, to enable proper representation of specialised archetypes. The ‘generated’ marker can be used on specialised archetypes - i.e. ADL 1.5 style .adls files - generated from flat archetypes - ADL 1.4 .adl files - and also in flat archetypes generated from differential files, by an inheritance-flattening process.

=== Controlled Indicator

A flag indicating whether the archetype is change-controlled or not can be included after the version, as follows:

---------------------------------------------------------------
    archetype (adl_version=2.0.5; controlled)
        org.openehr::openEHR-EHR-OBSERVATION.haematology.v1.2.0
---------------------------------------------------------------

This flag may have the two values "controlled" and "uncontrolled" only, and is an aid to software. Archetypes that include the "controlled" flag should have the revision history section included, while those with the "uncontrolled" flag, or no flag at all, may omit the revision history. This enables archetypes to be privately edited in an early development phase without generating large revision histories of little or no value.

== Specialise Section

This optional section indicates that the archetype is a specialisation of some other archetype, whose identity must be given. Only one specialisation parent is allowed, i.e. an archetype cannot ‘multiply-inherit’ from other archetypes. An example of declaring specialisation is as follows:

----------------------------------------------
    archetype (adl_version=2.0.5)
        openEHR-EHR-OBSERVATION.cbc.v1
    specialise 
        openEHR-EHR-OBSERVATION.haematology.v1
----------------------------------------------

Here the identifier of the new archetype is derived from that of the parent by adding a new section to its domain concept section. See the ` ARCHETYPE_ID` definition in the identification package in the openEHR Support IM specification.

Note that both the US and British English versions of the word "specialise" are valid in ADL.

The following syntax validity rule applies in the specialisation section:

SASID: archetype specialisation parent identifier validity. for specialised artefacts, the identifier of the specialisation parent must conform to the ARCHETYPE_ID identifier syntax defined in the openEHR Support IM Specification.

== Language Section

The `language` section includes meta-data describing the original language in which the archetype was authored (essential for evaluating natural language quality), and the total list of languages available in the archetype. There can be only one `original_language` . The `translations` list must be updated every time a translation of the archetype is undertaken. The following shows a typical example.

--------------------------------------------------------------------------
    language
        original_language = <[iso_639-1::en]>
        translations = <
            ["de"] = <
                language = <[iso_639-1::de]>
                author = <
                    ["name"] = <"Frederik Tyler">
                    ["email"] = <"freddy@something.somewhere.co.uk">
                >
                accreditation = <"British Medical Translator id 00400595">
            >
            ["ru"] = <
                language = <[iso_639-1::ru]>
                author = <
                    ["name"] = <"Nina Alexandrovna">
                    ["organisation"] = <"Dostoevsky Media Services">
                    ["email"] = <"nina@translation.dms.ru">
                >
                accreditation = <"Russian Translator id 892230-3A">
            >
        >
--------------------------------------------------------------------------

Archetypes must always be translated completely, or not at all, to be valid. This means that when a new translation is made, every language dependent section of the ` description` and ` terminology` sections has to be translated into the new language, and an appropriate addition made to the ` translations` list in the language section.

Note: some non-conforming ADL tools in the past created archetypes without a language section, relying on the terminology section to provide the original_language (there called primary_language) and list of languages (languages_available). In the interests of backward compatibility, tool builders should consider accepting archetypes of the old form and upgrading them when parsing to the correct form, which should then be used for serialising/saving.

== Description Section

The `description` section of an archetype contains descriptive information, or what some people think of as document "meta-data", i.e. items that can be used in repository indexes and for searching. The ODIN syntax is used for the description, as in the following example.

-------------------------------------------
    description
        original_author = <
            ["name"] = <"Dr J Joyce">
            ["organisation"] = <"NT Health Service">
            ["date"] = <2003-08-03>
        >
        lifecycle_state =  <"initial">
        resource_package_uri =  <"http://www.aihw.org.au/data_sets/diabetic_archetypes.html">
     
        details = <
            ["en"] = <
                language = <[iso_639-1::en]>
                purpose =  <"archetype for diabetic patient review">
                use = <"used for all hospital or clinic-based diabetic reviews, 
                    including first time. Optional sections are removed according to the particular review"
                >
                misuse = <"not appropriate for pre-diagnosis use">
                original_resource_uri = <"http://www.healthdata.org.au/data_sets/diabetic_review_data_set_1.html">
                other_details = <...>
            >
            ["de"] = <
                language = <[iso_639-1::de]>
                purpose =  <"Archetyp für die Untersuchung von Patienten mit Diabetes">
                use = <"wird benutzt für alle Diabetes-Untersuchungen im
                        Krankenhaus, inklusive der ersten Vorstellung. Optionale
                        Abschnitte werden in Abhängigkeit von der speziellen
                        Vorstellung entfernt."
                >
                misuse = <"nicht geeignet für Benutzung vor Diagnosestellung">
                original_resource_uri = <"http://www.healthdata.org.au/data_sets/diabetic_review_data_set_1.html">
                other_details = <...>
            >
        >
-------------------------------------------

A number of details are worth noting here. Firstly, the free hierarchical structuring capability of ODIN is exploited for expressing the ‘deep’ structure of the `details` section and its subsections. Secondly, the ODIN qualified list form is used to allow multiple translations of the `purpose` and `use` to be shown. Lastly, empty items such as `misuse` (structured if there is data) are shown with just one level of empty brackets. The above example shows meta-data based on the openEHR Archetype Object Model (AOM).

The `description` section is technically optional according to the AOM, but in any realistic use of ADL for archetypes, it will be required. A minimal description section satisfying to the AOM is as follows:

--------------------------------------------------------------------------
    description
        original_author = <
            ["name"] = <"Dr J Joyce">
            ["organisation"] = <"NT Health Service">
            ["date"] = <2003-08-03>
        >
        lifecycle_state = <"initial">
        details = <
            ["en"] = <
                language = <[iso_639-1::en]>
                purpose = <"archetype for diabetic patient review">
            >
        >
--------------------------------------------------------------------------

== Deprecated Sections

=== Concept Section

A ‘concept’ section was required up until ADL 1.4. In ADL 1.5, the concept section is deprecated, but allowed, enabling ADL 1.4 archetypes to be treated as valid. It will be removed in a future version of ADL, since it is completely redundant.

All archetypes represent some real world concept, such as a "patient", a "blood pressure", or an "ante-natal examination". The concept is always coded, ensuring that it can be displayed in any language the archetype has been translated to. A typical ` concept` section is as follows:

----
concept [at0000] -- haematology result
----

In this concept definition, the term definition of ` [at0000]` is the proper description corresponding to the "haematology-cbc" section of the archetype identifier above.

The following syntax validity rule applies to the concept section, if present, allowing parsers to correctly ignore it:

[.rule]
SACO: archetype concept validity: if a concept section is present, it must consist of the ‘concept’ keyword and a single local term.

== Definition Section

The ` definition` section contains the main formal definition of the archetype, and is written in the Constraint Definition Language (cADL). A typical ` definition` section is as follows:

-------------
    definition
        OBSERVATION[id1] ∈ {                                                 -- blood pressure measurement
            name ∈ {                                                         -- any synonym of BP
                DV_CODED_TEXT[id2] ∈ {
                    defining_code ∈ { 
                        CODE_PHRASE[id3] ∈ {[ac1]}
                    }
                }
            }
            data ∈ {            
                HISTORY[id4] ∈ {                                              -- history
                    events cardinality ∈ {1..*} ∈ { 
                        POINT_EVENT[id5] occurrences ∈ {0..1} ∈ {            -- baseline
                            name ∈ {
                                DV_CODED_TEXT[id6] ∈ {
                                    defining_code ∈ {
                                        CODE_PHRASE[id7] ∈ {[ac2]}
                                    }
                                }
                            }
                            data ∈ {
                                ITEM_LIST[id8] ∈ {                              -- systemic arterial BP
                                    items cardinality ∈ {2..*} ∈ {
                                        ELEMENT[id9] ∈ {                        -- systolic BP
                                            name ∈ {                            -- any synonym of 'systolic'
                                                DV_CODED_TEXT[id10] ∈ {
                                                    defining_code ∈ {
                                                        CODE_PHRASE[id11] ∈ {[ac2]}
                                                    }
                                                }
                                            }
                                            value ∈ {
                                                DV_QUANTITY[id12] ∈ {
                                                    magnitude ∈ {0..1000}
                                                    property ∈ {[properties::944]}  -- "pressure"
                                                    units ∈ {[units::387]}          -- "mm[Hg]"
                                                }
                                            }
                                        } 
                                        ELEMENT[id79] ∈ {                            -- diastolic BP
                                            name ∈ {                             -- any synonym of 'diastolic'
                                                DV_CODED_TEXT[id14] ∈ {
                                                    defining_code ∈ {
                                                        CODE_PHRASE[id15] ∈ {[ac3]}
                                                    }
                                                }
                                            }
                                            value ∈ {
                                                DV_QUANTITY[id16] ∈ {
                                                    magnitude ∈ {0..1000}
                                                    property ∈ {[properties::944]}   -- "pressure"
                                                    units ∈ {[units::387]}           -- "mm[Hg]"
                                                }
                                            }
                                        } 
                                        ELEMENT[id17] occurrences ∈ {0..*} ∈ {*}    -- unknown new item
                                    }
                                ...
-------------

This definition expresses constraints on instances of the types `ENTRY` , `HISTORY` , `EVENT` , `ITEM_LIST` , `ELEMENT` , `QUANTITY` , and `CODED_TEXT` so as to allow them to represent a blood pressure measurement, consisting of a history of measurement events, each consisting of at least systolic and diastolic pressures, as well as any number of other items (expressed by the `[at9000]` "any" node near the bottom).

=== Design-time and Run-time paths

All archetype object constraint nodes require a node identifier. When data are created according to the ` definition` section of an archetype, the archetype node identifiers can be written into the data, providing a reliable way of finding data nodes, regardless of what other runtime names might have been chosen by the user for the node in question. There are two reasons for doing this. Firstly, querying cannot rely on runtime names of nodes (e.g. names like "sys BP", "systolic bp", "sys blood press." entered by a doctor are unreliable for querying); secondly, it allows runtime data retrieved from a persistence mechanism to be re-associated with the cADL structure which was used to create it.

An example which shows the difference between design-time meanings associated with node identifiers and runtime names is the following, from a ` SECTION` archetype representing the problem/SOAP headings (a simple heading structure commonly used by clinicians to record patient contacts under top-level headings corresponding to the patient’s problem(s), and under each problem heading, the headings "subjective", "objective", "assessment", and "plan").

--------------------------------------
    SECTION[id1] matches {                          -- problem
        name matches {
            DV_CODED_TEXT[id2] matches {
                defining_code matches {[ac1]}       -- any clinical problem type
            }
        }
    }
--------------------------------------

In the above, the node identifier `[id1]` is assigned a meaning such as "clinical problem" in the archetype terminology section. The subsequent lines express a constraint on the runtime _name_ attribute, using the internal code `[ac1]` . The constraint `[ac1]` is also defined in the archetype terminology section with a formal statement meaning "any clinical problem type", which could clearly evaluate to thousands of possible values, such as "diabetes", "arthritis" and so on. As a result, in the runtime data, the node identifier corresponding to "clinical problem" and the actual problem type chosen at runtime by a user, e.g. "diabetes", can both be found. This enables querying to find all nodes with meaning "problem", or all nodes describing the problem "diabetes". Internal `[acNNNN]` codes are described in link:main.htm#70808[See Local Constraint Codes].

== Rules Section

The ` rules` section in an ADL archetype introduces assertions which relate to the entire archetype, and can be used to make statements which are not possible within the block structure of the ` definition` section. Any constraint which relates more than one property to another is in this category, as are most constraints containing mathematical or logical formulae. Rules are expressed in the archetype assertion language, described in link:assertion.htm#68334[See Assertions].

An assertion is a first order predicate logic statement which can be evaluated to a boolean result at runtime. Objects and properties are referred to using paths.

The following simple example says that the speed in kilometres of some node is related to the speed-in-miles by a factor of 1.6:

-----------------------------------------------------------
rules
    validity: /speed[id2]/kilometres[id13]/magnitude = /speed[id4]/miles[id14]/magnitude * 1.6
-----------------------------------------------------------

== Terminology Section

=== 8.11.1 ADL 1.4 / 1.5 Version Compatibility

--------------------------------------
    term_definitions = <
        ["en"] = <
            items = <
                ["at0001"] = <...>
                ["at0002"] = <...>
            >
        >
    >
--------------------------------------

=== 8.11.2 Overview

The ` terminology` section of an archetype is expressed in ODIN, and is where codes representing node IDs, constraints on text or terms, and bindings to terminologies are defined. Linguistic language translations are added in the form of extra blocks keyed by the relevant language. The following example shows the layout of this section.

-------------------------------
terminology 
    term_definitions = <
        ["en"] = <
            ["id1"] = <...>
            ["at1"] = <...>
            ["ac1"] = <...>
        >
        ["de"] = <
            ["id1"] = <...>
            ["at1"] = <...>
            ["ac1"] = <...>
        >
    >
    term_bindings = <
        ["snomed_ct"] = <
            ["id4"] = <...>
            ["ac1"] = <...>
            ...
        >
    >
-------------------------------

The ` term_definitions` section is mandatory, and must be defined for each translation carried out.

=== 8.11.3 Term_definitions Section

This section is where all archetype local terms (including all at-codes, ac-codes, and at least the id-codes attached to container attribute children) are defined. The following example shows an extract from the English and German term definitions for the archetype local terms in a problem/SOAP headings archetype. Each term is defined using a structure of name/value pairs, and mustat least include the names "text" and "description", which are akin to the usual rubric, and full definition found in terminologies like SNOMED-CT. Each term object is then included in the appropriate language list of term definitions, as shown in the example below.

------
    term_definitions = <
        ["en"] = <
            ["id1"] = <
                text = <"problem">
                description = <"The problem experienced by the subject 
                    of care to          which the contained information relates">
            >
            ["id2"] = <
                text = <"problem/SOAP headings"> 
                description = <"SOAP heading structure for multiple problems">
            >
            ... 
            ["id3"] = <
                text = <"plan">
                description = <"The clinician's professional advice">
            >
        >
    ["de"] = <
        ["id1"] = <
                text = <"klinisches Problem">
                description = <"Das Problem des Patienten worauf sich diese \
                            Informationen beziehen">
        >
        ["id2"] = <
                text = <"Problem/SOAP Schema"> 
                description = <"SOAP-Schlagwort-Gruppierungsschema fuer
                        mehrfache           Probleme">
        >
        ["id3"] = <
                text = <"Plan">
                description = <"Klinisch-professionelle Beratung des
                            Pflegenden">
        >
    >
------

In some cases, term definitions may have been lifted from existing terminologies (only a safe thing to do if the definitions exactly match the need in the archetype). To indicate where definitions come from, a "provenance" tag can be used, as follows:

----
    ["id3"] = <
        text = <"plan">; description = <"The clinician's professional advice">; provenance = <"ACME_terminology(v3.9a)"> 
    >
----

Note that this does not indicate a _binding_ to any term, only its origin. Bindings are described in link:main.htm#39544[See Term_bindings Section].

The `term_definitions` section also includes definitions for archetype-local constraint codes, which are of the form ` [acN]` . Each such code refers to some constraint such as "any term which is a subtype of ‘hepatitis’ in the ICD9AM terminology"; the constraint definitions do not provide the constraints themselves, but define the _meanings_ of such constraints, in a manner comprehensible to human beings, and usable in GUI applications. This may seem a superfluous thing to do, but in fact it is quite important. Firstly, term constraints can only be expressed with respect to particular terminologies - a constraint for "kind of hepatitis" would be expressed in different ways for each terminology which the archetype is bound to. For this reason, the actual constraints are defined in the ` term_bindings` section. An example of a constraint term definition for the hepatitis constraint is as follows:

------------------------------------------------------------------------
    ["ac1015"] = <
        text = <"type of hepatitis">
        description = <"any term which means a kind of viral hepatitis">
    >
------------------------------------------------------------------------

Note that while it often seems tempting to use classification codes, e.g. from the ICD vocabularies, these will rarely be much use in terminology or constraint definitions, because it is nearly always _descriptive_ , not classificatory terms which are needed.

=== 8.11.4 Term_bindings Section

This section is used to describe the equivalences between archetype local terms and terms and value sets found in external terminologies. Bindings are expressed as URIs.

1.  note that the actual URIs here are probably not yet correct; refer to IHTSDO / Mayo / CTS2 etc

The main purpose for allowing query engines to search for an instance of some external term to determine what equivalent to use in the archetype.

Global Term Bindings

There are two types of term bindings that can be used, ‘global’ and path-based. The former is where an external term is bound directly to an archetype local term, and the binding holds globally throughout the archetype. In many cases, archetype terms only appear once in an archetype, but in some archetypes, at-codes are reused throughout the archetype. In such cases, a global binding asserts that the correspondence is true in all locations. A typical global term binding section resembles the following:

----------------------------------------------------------------------
    term_bindings = <
        ["umls"] = <
            ["id1"] = <http://umls.nlm.edu/id/C124305> -- apgar result
            ["id2"] = <.../0000000> -- 1-minute event 
            ["id4"] = <.../C234305> -- cardiac score
            ["id5"] = <.../C232405> -- respiratory score
            ["id6"] = <.../C254305> -- muscle tone score
            ["id7"] = <.../C987305> -- reflex response score
            ["id8"] = <.../C189305> -- color score
            ["id9"] = <.../C187305> -- apgar score
            ["id10"] = <.../C325305> -- 2-minute apgar
            ["id11"] = <.../C725354> -- 5-minute apgar
            ["id12"] = <.../C224305> -- 10-minute apgar
        >
    >
----------------------------------------------------------------------

Each entry indicates which term in an external terminology is equivalent to the archetype internal codes. Note that not all internal codes necessarily have equivalents: for this reason, a terminology binding is assumed to be valid even if it does not contain all of the internal codes.

Path-based Bindings

The second kind of binding is one between an archetype path and an external code. This occurs commonly for archetypes where a term us re-used at the leaf level. For example, in the binding example below, the id4 code represents ‘temperature’ and the codes id3, id5, id6 etc correspond to various times such as ‘any’, 1-hour average, 1-hour maximum and so on. Some terminologies (notably LOINC, the laboratory terminology in this example) define ‘pre-coordinated’ codes, such as ‘1 hour body temperature’; these clearly correspond not to single codes such as id4 in the archetype, but to whole paths. In such cases, the key in each term binding row is a full path rather than a single term.

------------------
    ["LNC205"] = <
        ["/data[id2]/events[id3]/data[id1]/item[id4]"] = <http://loinc.org/ver=LNC205/id=8310-5>
        ["/data[id2]/events[id5]/data[id1]/item[id4]"] = <.../8321-2>
        ["/data[id2]/events[id6]/data[id1]/item[id4]"] = <.../8311-3>
        ["/data[id2]/events[id7]/data[id1]/item[id4]"] = <.../8316-2>
        ["/data[id2]/events[id8]/data[id1]/item[id4]"] = <.../8332-0>
        ["/data[id2]/events[id9]/data[id1]/item[id4]"] = <.../8312-1>
        ["/data[id2]/events[id17]/data[id1]/item[id4]"] = <.../8325-3>
        ["/data[id2]/events[id19]/data[id1]/item[id4]"] = <.../8320-4>
    >
------------------

Bindings to external value sets are also included in the bindings section, also as URIs:

--------
    term_bindings = < 
        ["snomed_ct"]    = <         
            ["ac1"] = <http://terminology.org?query_id=12345>
            ["ac2"] = <http://terminology.org?query_id=678910>
        >
    >
--------

In this example, each local constraint code is formally defined to refer to a query defined in a terminology service, in this case, a terminology service that can interrogate the Snomed-CT terminology.

=== 8.11.5 Deprecated Elements

In the 1.4 release of ADL, there were two separate bindings sections, `term_bindings ` and `constraint_bindings` . These have been merged into one section, `bindings` .

Additionally in ADL 1.4, a `terminologies_available` header statement was required to identify all terminologies for which `term_bindings` sections have been written. For example:

terminologies_available = <"snomed_ct", "loinc">

This is no longer required. In archetypes that have it, it is ignored, and should not be included in ADL 1.5 or later syntax output serialisation.

== Annotations Section

The annotations section of an archetype or template provides a place for node-level meta-data to be added to the archetype. This can be used during the design phase to track dependencies, design decisions, and specific resource references. Each annotation is keyed by the path of the node being annotated, or a pure RM path, and may have any number of tagged elements. A typical annotations section looks as follows.

---------
annotations 
    items = <
        ["en"] = <
            items = <
                ["/data/items[at0.37]/items[at0.38]/value"] = < -- Clin st. / stage
                    items = <
                        ["messaging requirement"] = <"= ‘staging’ field in msg type 2345">
                        ["guideline"] = <"http://guidelines.org/gl24.html#staging">
                        ["data dict equivalent"] = <"NHS data item aaa.1">
                    >
                >
                ["/data/items[at0.37]/items[at0.39]/value"] = < -- Clin st. / Tumour
                    items = <
                        ["message requirement"] = <"tumour field in msg type 2345">
                        ["guideline"] = <"http://guidelines.org/gl24.html#mass">
                        ["data dict equivalent"] = <"NHS data item aaa.2">
                    >
                >
            >
        >
    >
---------

Annotations are defined as a separate section that can be easily removed in production versions of an archetype or template, and/or ignored in the generation of digital signatures.

Typically annotations are used to document a particular node within an archetype, specified by its (unique) archetype path. In some cases, the archetype (or tempate) author wants to document the use of a reference model attribute that is not constrained in the archetype (and therefore does not have an archetype path as such). In this case, the path will just be a valid RM path, i.e. a path relative to the top-level object of the archetype, and containing no at-codes.

== Revision History Section

The revision history section of an archetype shows the audit history of changes to the archetype, and is expressed in ODIN syntax. It is optional, and is included at the end of the archetype, since it does not contain content of direct interest to archetype authors, and will monotonically grow in size. Where archetypes are stored in a version-controlled repository such as CVS or some commercial product, the revision history section would normally be regenerated each time by the authoring software, e.g. via processing of the output of the ‘prs’ command used with SCCS files, or ‘rlog’ for RCS files. The following shows a typical example, with entries in most-recent-first order (although technically speaking, the order is irrelevant to ADL).

----------------------------------------------------------------
revision_history 
    revision_history = <
        ["1.57"] = <
            committer = <"Miriam Hanoosh">
            committer_organisation = <"AIHW.org.au">
            time_committed = <2004-11-02 09:31:04+1000>
            revision = <"1.2">
            reason = <"Added social history section">
            change_type = <"Modification">
        >
        -- etc
        ["1.1"] = <
            committer = <"Enrico Barrios">
            committer_organisation = <"AIHW.org.au">
            time_committed = <2004-09-24 11:57:00+1000>
            revision = <"1.1">
            reason = <"Updated HbA1C test result reference">
            change_type = <"Modification">
        >
        ["1.0"] = <
            committer = <"Enrico Barrios">
            committer_organisation = <"AIHW.org.au">
            time_committed = <2004-09-14 16:05:00+1000>
            revision = <"1.0">
            reason = <"Initial Writing">
            change_type = <"Creation">
        >
    >
----------------------------------------------------------------

= Terminology Integration

This section describes how archetype constraints on terminological entities can be expressed in their various forms, and how these constraints are resolved to concrete sets of terms.

== Source-form Constraints
 

== Expanded Constraints


== Constraint Resolution

Terminology constraints are an alternative to enumeration of value sets within an archetype. Inline enumeration will work perfectly well in a technical sense, but has at least two limitations. Firstly, the intended set of values allowed for the attribute may change over time (e.g. as has happened with ‘types of hepatitis’ since 1980), and since the authoritative resource is elsewhere, the archetype has to be continually updated. With a large repository of archetypes, each containing inline coded term constraints, this approach is likely to be unsustainable and error-prone. Secondly, the best means of defining the value set is in general not likely to be via enumeration of the individual terms, but in the form of a semantic expression that can be evaluated against the external resource. This is because the value set is typically logically specified in terms of inclusions, exclusions, conjunctions and disjunctions of general categories.

[.discussion]
Consider for example the value set logically defined as “any bacterial infection of the lung”. The possible values would be codes from a target terminology, corresponding to numerous strains of pneumococcus, staphlycoccus and so on, but not including species that are never found in the lung. Rather than enumerate the list of codes corresponding to this value set (which is likely to be quite large), the archetype author is more likely to rely on semantic links within the external terminology to express the set; a query such as ‘is-a bacteria and has-site lung’ is definable against the SNOMED-CT terminology for example.

[.discussion]
In a similar way, other value sets, including for quantitative values, are likely to be specified by queries or formal expressions, and evaluated by an external knowledge service. Examples include "any unit of pressure" and "normal range values for serum sodium".

[.discussion]
In such cases, expressing the terminology constraint could be done by including the query or other formal expression directly within the archetype itself. However, experience shows that this is problematic in various ways. Firstly, there is little if any standardisation in such formal value set expressions or queries for use with knowledge services - two archetype authors could easily create competing syntactical expressions for the same logical constraint. A second problem is that errors might be made in the query expression itself, or the expression may be correct at the time of authoring, but need subsequent adjustment as the relevant knowledge resource grows and changes. The consequence of this is the same as for a value set enumerated inline - it is unlikely to be sustainable for large numbers of archetypes.

[.discussion]
These problems are not accidental: a query with respect to a terminological, ontological or other knowledge resource is most likely to be authored correctly by maintainers or experts of the knowledge resource, rather than archetype authors; it may well be altered over time due to improvements in the query formalism itself.

 
